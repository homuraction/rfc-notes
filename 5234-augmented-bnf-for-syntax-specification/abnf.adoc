:lang: ja
:doctype: book
:toc: left
:toc-title: 目次
:sectnums:
:sectlinks:
:icons: font
:source-highlighter: coderay
:exmaple-caption: 例
:table-caption: 表
:figure-caption: 図
:author: homuraction
:revnumber: 0.1
:revdate: 2018/08/19

= Augmented BNF for Syntax Specifications: ABNF

== Abstract

インターネット技術仕様はしばしば正式な構文を定義する必要がある.
長年に渡り、多くのインターネット仕様の中では、あるBackus-Naur Form (BNF) の修正されたバージョンでありAugumented BNF (ABNF) と呼ばれるものが普及してきた.
現在の仕様文書にはABNFが記載されている.
それは合理的な表現力を持ちつつもコンパクトさ及び単純さにおいてバランスが取れている.
標準的なBNFとABNF間の差異は命名規則、繰り返し、代替案、順序独立性及び値範囲に関係している.
この仕様は追加規則定義と多くのインターネット仕様に対する共通タイプのコア字句解析器の符号化も提供する.

== Introduction

(Abstract略)

近年のArpanetでは、互いの仕様がそれ自身のABNF定義を含んでいた.
これはRFC733及び次のRFC822のemail仕様にも含んだものであり、それはABNFを定義するための一般的な引用となった.
現在の文書は選択的な参照を可能とするためそれらの定義を切り分ける.
予想通り、それはいくつかの変更や強化も提供する.

(Abstract略)

付録Bにて追加規則定義及び符号化に関する記述を提供する.
それは便宜的に提供される、或いはこの文書のBody部に定義されたメタ言語から切り離され、そしてその正式な地位から切り離される.

== Rule Definition

=== Rule Naming

規則の名前は単純に名前そのものであり、それは文字の並びであり、アルファベット文字から始まり、その後アルファベット、数字及びハイフン(ダッシュ)が続く.

[NOTE]
規則の名前はケースインセンシティブである

<rulename> / <Rulename> / <RULENAME> / <rUlENamE> +
これらは全て同じ規則を参照する.

オリジナルのBNFとは異なり、山形括弧("<", ">")は必須ではない. +
しかしながら、山形括弧はその存在が規則名の使用を識別するのを助ける時はいつでも規則名の周辺に用いられるだろう.
これは典型的には自由形式の散文における規則名の参照に制限されるか、もしくは部分規則を見分けるために制限される.
その部分規則は、後述する繰り返しに関する議論に見られるように、空白によって切り分けられていない文字列を結合させる.

=== Rule Form

規則は下記の順序によって定義される:

```
  name = elements crlf
```

<name>の場所は規則名であり、<elements>は1つ以上の規則名もしくは終端仕様であり、<crlf>は改行を示すものである(改行に続くキャリッジリターン).
等号文字は規則の定義とその名前を分けている.
elementsは1以上の規則名且つ/もしくは値定義の列であり、代替や繰り返しなどのようにこの文書内で定義されている様々な演算子に応じて結合される.

視覚的見易さのため、規則定義は左寄せとする.
規則が複数の行を必要とする時、継続行はインデントされる.
左寄せとインデントはABNF規則の1行目に対する相対的なものであり、文書の左余白に一致する必要はない.

=== Terminal Values

規則が終端値の文字列に解決される、それは時々charactersと呼ばれる.
ABFNにおいて、一つの文字は単に一つの非負の整数である.
特定の文脈においては、ある値から文字セット(ASCIIのような)への特別なマッピング(符号化)が指定される.

終端子は1つ以上の数字で指定され、それらの文字の進数解釈は明示的に示されている.
下記の進数が現在定義されている:

```
  b = binary
  d = decimal
  x = hexadecimal
```

それ故:

```
  CR = %d13
  CR = %x0D
```

それぞれはUS-ASCIIにおけるキャリッジリターンの表現として10進数と16進数を指定している.

その様な値の連結された文字列は、その値の中で文字列が区切られている事を示すためにピリオド(".")を使ってコンパクトに指定される.それ故:

```
  CRLF = %d13.10
```

ABNFはクォートマークで囲まれたリテラルテキスト文字列の仕様を直接許可する.それ故:

```
  command = "command string"
```

リテラルテキスト文字列は印字可能な文字群の集合が連結されたものとして解釈される.

[NOTE]
ABNF文字列はケースインセンシティブでありこの文字列の文字集合はUS-ASCIIである

それ故:

```
  rulename = "abc"
```

且つ:

```
  rulename = "aBc"
```

上記は "abc", "Abc", "aBc", "abC", "ABc", "aBC", "AbC"そして"ABC"と一致する.

規則をケースセンシティブに指定するためには、その文字を個別に指定する.

例えば:

```
  rulename = %d97 %d98 %d99
or
  rulename = %d97.98.99
```

上記は小文字の文字群であるabcのみを含む文字列にだけ一致する.

=== External Encodings

終端子文字の外部表現はストレージや伝送環境内の制約によって変化する.
そのため、同じABNFベースな文法は複数の外部表現形式を持つかもしれず、例えば一方は7ビットUS-ASCII環境であり、もう一方はバイナリオクテット環境であり、そして更に異なる一方は16ビットUnicodeが使われるかもしれない.
付録Bはインターネットに普遍的にマッチしてきた環境として7ビットUS-ASCIIを提供するものの、表現形式の詳細はABNFのスコープから外れている.

構文から外部表現形式を切り離す事により、それは代替の符号化環境が同じ構文として用いられ得る事を意図している.

== Operation

=== Concatenation: Rule1 Rule2

一連の規則名を列挙する事によって順序付けられた値の文字列(例えば連続連結文字)という様に、とある規則は簡単に定義できる.例えば:

```
  foo    = %x61        ; a
  bar    = %x62        ; b
  mumble = foo bar foo
```

そのため、規則<mumble>は小文字文字列である"aba"と一致する.

線形空白: 連結はABNF解析モデルのコアに位置する.
ある連続文字(値)の文字列はABNFで定義された規則に応じて解析される.
インターネット仕様により、特殊な区切り文字やアトミック文字列などのような、主要なコンストラクトの周りに自由かつ暗黙の内に散在する幾つかの許可された線形空白(空白及び水平タブ)の歴史がある.

[NOTE]
このABNFのための仕様は線形空白の暗黙な仕様を提供しない

デリミタや文字列セグメント周りの線形空白が許可される事を望む任意の文法はそれを明示的に指定しなければならない.
上位規則内において様々に使用されるような中核となる規則においてそのような空白を提供する事はしばしば便利である.
その中核となる規則は字句解析器に形成されるかもしれない、もしくは単に主要な規則集合の一部となるかもしれない.

=== Alternatives Rule1 / Rule2

スラッシュ("/")により区切られたelementsは代替である.したがって:

```
  foo / bar
```

上記は<foo>または<bar>を受容する

[NOTE]
クォートで囲まれたアルファベットを含んでいる文字列は代替文字を指定する特別な形式であり文字を含む文字列の組み合わせ集合である非終端子表現であると解釈され、その集合の要素は指定された順序で大文字と小文字が混在している文字列である.

=== Incremental Alternatives: Rule1 =/ Rule2

フラグメント内にて代替のリストを指定する事は時々便利である.
代替集合に加える後続の規則定義を用いてある初期規則は1つ以上の代替と一致するかもしれない.
これはパラメータリストを用いてしばしば引き起こされる同じ親の規則集合から派生したその他の独立した仕様にとって特に有用である.
ABNFはこのインクリメンタル定義をコンストラクトを通して許可する:

```
  oldrule =/ additional-alternatives
そのため規則集合は
  ruleset = alt1 / alt2
  ruleset =/ alt3
  ruleset =/ alt4 / alt5
上記は下記を指定する事と同様である
  ruleset = alt1 / alt2 / alt3 / alt4 / alt5
```

=== Value Range Alternatives: %c#\#-##

代替値の範囲を示すためにダッシュ("-")を使用することで代替数値範囲はコンパクトに指定されうる.それ故:

```
  DIGIT = %30-39
上記は下記と同等である:
  DIGIT = "0" / "1" / "2" / "4" / "5" / "6" / "7" / "8" / "9"
```

連結された数値や数値範囲は同じ文字列として指定される事ができない.
ある数値は連結のために点線記法を用いるかもしれない、または、それはダッシュ記法を使って1つの値の範囲を指定するかもしれない.
それ故、行末シーケンス間で1つの印字可能な文字を指定するために、その仕様は下記の様に記述可能である:

```
  char-line = %x0D.0A %x20-7E %x0D.0A
```

=== Sequence Group: (Rule1 Rule2)

括弧で囲まれたelementsは単一のelementとして取り扱われ、そのコンテンツは強く順序付けられる.したがって:

```
  elem (foo / bar) blat
上記は(elem foo blet)または(elem bar blet)と一致し、
  elem foo /bar blat
上記は(elem foo)または(bar blat)と一致する
```

[NOTE]
代替が複数の規則名やリテラルを含む時は、剥き出しの代替を正しく読む事を信頼するよりも、グルーピング記法が用いられる事が強く推奨される

それ故、下記の形式が用いられる事を推奨する:

```
  (elem foo) / (bar blat)
```

これは軽く読み通す者による誤った解釈を回避させるだろう.

シーケンスグループ記法は散文からelement列設定するために自由テキスト内でも用いられる

=== Variable Repetition: *Rule

あるelementに先行する"*"演算子は反復を示す.
その完全な形式は下記である:

```
  <a>*<b>element
```

<a>と<b>の箇所はオプショナルな10進数値であり、少なくとも<a>且つ最大<b>回出現する事を示している.

デフォルト値は0と無限値であり、したがって `*<element>` は0を含む任意の回数を受容する; `1*<element>` は最低1以上である回数を要求する; `3*3<element>`は単に3回を要求する; そして `1*2<element>` は1回または2回の反復を許可する.

=== Specific Repetition: nRule

```
とある規則の形式
  <n>element
は下記と同等である
  <n>*<n>element
```

これは、まさしくelementのn回出現である.
したがって、2DIGITは2桁の数値、そして、3ALPHAは三文字のアルファベット文字からなる文字列である.

=== Optional Sequence: [RULE]

```
下記に示す角括弧で囲まれたオプショナルelement列は
  [foo bar]
下記と同等である
  *1(foo bar)  ; 0回か1回出現する <=> Optional
```

=== Comment: ; Comment

セミコロンはコメントを開始し、それは行末まで続く.
これは仕様と並行して有用なノートを含ませる簡単な方法である.

=== Operator Precedence

上述した様々なメカニズムは以下の優先順位を持つ、最上位(最も束縛が厳しい)である最上段から、最下位(もっとも緩い)である最下段まで:

. 規則名, 散文値, 終端子
. コメント
. 値範囲
. 反復
. グルーピング, オプショナル
. 連結
. 代替

代替演算子と連結を混ぜ合わせて自由に使う事は混乱を招き得る.

繰り返すが、連結グループを明示するためにグルーピング記法が用いられる事を推奨する.

== ABNF Definition of ABNF

=== Syntax Requirements

この構文は比較的厳しい規則の書式設定を要求する.
それ故、仕様に含まれる規則集合のバージョンはABNF解析器によって解釈可能である事を保障するために前処理を必要とする.

=== Rules

この構文は付録Bで提供される規則を使用する

```
rulelist      = 1*( rule / (*c-wsp c-nl) )
rule          = rulename defined-as elements c-nl
                     ; continues if next line starts
                     ;   with white space
rulename      = ALPHA *(ALPHA / DIGIT / "-")
defined-as    = *c-wsp ("=" / "=/") *c-wsp
elements      = alternation *c-wsp
c-wsp         = WSP / (c-nl WSP)
c-nl          = comment / CRLF
                     ; comment or newline
comment       = ";" *(WSP / VCHAR) CRLF
alternation   = concatenation
                  *(*c-wsp "/" *c-wsp concatenation)
concatenation = repetition *(1*c-wsp repetition)
repetition    = [repeat] element
repeat        = 1*DIGIT / (*DIGIT "*" *DIGIT)
element       = rulename / group / option /
                  char-val / num-val / prose-val
group         = "(" *c-wsp alternation *c-wsp ")"
option        = "[" *c-wsp alternation *c-wsp ")"
char-val      = DQUOTE *(%x20-21 / %x23-7E) DQUOTE
num-val       = "%" (bin-val / dec-val / hex-val)
bin-val       = "b" 1*BIT
                  [ 1*("." 1*BIT) / ("-" 1*BIT) ]
                       ; series of concatenated bit values
                       ;   or single ONEOF range
dec-val       = "d" 1*DIGIT
                  [ 1*("." 1*DIGIT) / ("-" 1*DIGIT)]
hex-val       = "x" 1*HEXDIG
                  [ 1*("." 1*HEXDIG) / ("-" 1*HEXDIG)]
prose-val     = "<" *(%x20-3D / %x3F-7E) ">"
                  ; bracketed string of SP and VCHAR
                  ;   without angles
                  ; prose description, tobe used as
                  ;   last resort
```

付録B読んでWSPの定義とか書くの面倒なのでここで一旦Done
